plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'org.beryx.jlink' version '2.17.3'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

group 'me.matrix4f.cardcutter'
version '2.0.0'
mainClassName = 'me.matrix4f.cardcutter.launcher.CardifyLauncherMainKt'

repositories {
    mavenCentral()
}

idea {
    module {
        inheritOutputDirs = true
    }
}

javafx {
    modules = [ 'javafx.controls', 'javafx.base', 'javafx.graphics' ]
}

jar {
    manifest { attributes 'Main-Class': mainClassName }
}

jlink {
    //noinspection GroovyAssignabilityCheck, GrFinalVariableAccess, GroovyAccessibility
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'CardifyLauncher'
    }

    forceMerge 'commons-exec'
    forceMerge 'log4j-api'

    jpackageImage.doLast {
        copy {
            from "C:\\Users\\vgsoh\\Desktop\\CC Extension\\native"
            into "build/jpackage/CardifyLauncher"
            include "dlls/**"
        }
    }

    jpackage {
        jpackageHome = "C:\\Program Files\\Java\\jdk-14"
        appVersion = '0.0.1'

        imageOptions = [
                '--icon', 'X:\\dev\\intellij\\CardifyDebate\\CardifyExtension\\icons\\icon.ico'
        ]

        installerOptions = [
                '--type', 'msi',

                '--win-menu',
                '--win-menu-group', 'CardifyDebate',
                '--win-per-user-install',
                '--win-upgrade-uuid', '74252fd4-d645-4244-a2fb-ee69474db56f',
                '--win-shortcut',
                '--copyright', 'Copyright Soham Govande 2019-2020',
                '--vendor', 'CardifyDebate, Inc'
        ]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.71"
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'
    implementation 'org.apache.commons:commons-exec:1.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.apache.logging.log4j:log4j-api:2.13.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.0'
    implementation 'commons-io:commons-io:2.5'
    implementation 'net.lingala.zip4j:zip4j:2.3.0'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}